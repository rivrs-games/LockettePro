apply plugin: 'java-library'
apply plugin: 'maven-publish'

// Suppiled by Jenkins
ext.majorVersion = 2
ext.minorVersion = 10
ext.minecraftVersion = "1.16.1"
ext.buildNumber = System.env.BUILD_NUMBER == null ? "x"                : "$System.env.BUILD_NUMBER"
ext.mavenDirectory = System.env.MAVEN_DIR == null ? "$projectDir/repo" : "$System.env.MAVEN_DIR"
ext.jdDirectory = System.env.JAVADOCS_DIR == null ? null               : "$System.env.JAVADOCS_DIR"

sourceCompatibility = 1.8
targetCompatibility = 1.8

// differet version convension from Nyaa plugins
group = "me.crafter.mc"
archivesBaseName = "LockettePro"
version = "$majorVersion.$minorVersion.${buildNumber}-mc$minecraftVersion"

repositories {
    jcenter()
    maven {name 'Spigot';     url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'}
    maven {name 'Sonatype';   url 'https://oss.sonatype.org/content/groups/public'}
    maven {name 'sk89q';      url 'http://maven.sk89q.com/artifactory/repo/'; allowInsecureProtocol(true)}
    maven {name 'playpro';    url 'https://maven.playpro.com/'}
    maven {name 'vault-repo'; url 'https://jitpack.io'}
    maven {name 'dmulloy2';   url 'http://repo.dmulloy2.net/content/groups/public/'; allowInsecureProtocol(true)}
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.16.1-R0.1-SNAPSHOT'
    compileOnly('net.milkbowl.vault:VaultAPI:1.7') { transitive = false }
    compileOnly('com.sk89q.worldguard:worldguard-bukkit:7.0.2-SNAPSHOT') { transitive = false }
    // FIXME: ProtocolLib-API is not available for 4.5.0
    // Note: the repo maven.playpro.com is not updated to match the version on spigotmc
    compileOnly('net.coreprotect:coreprotect:19.1') { transitive = false }
}
compileJava {
    options.compilerArgs += ["-Xlint:deprecation"]
}
processResources {
    expand version: project.version
}

// maven publications
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.java.srcDirs
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact sourcesJar
            artifactId "lockettepro"
            version "$majorVersion.$minorVersion-SNAPSHOT"
            from components.java
        }
    }
    repositories {
        maven {
            url "$mavenDirectory"
        }
    }
}
